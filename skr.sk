
	# # # # # # # # # # # # # # # # #
	#								#
	#		SKRIPT RELOAD			#
	#		By Frost_145#8093		#
	#								#
	# # # # # # # # # # # # # # # # #

	# Skript réalisé par Frost_145#8093 !
	# Veuillez ne pas vous approprier ce code comme étant le vôtre afin de respecter mon travail, merci.
	#
	#	Requis:
	#	- Skript
	#	- SkUtilities			| https://docs.skunity.com/addon/skUtilities
	#	- SkQuery				| https://forums.skunity.com/resources/unofficial-skquery-fork-1-6-1-12.68/updates

options:
	# # # # # # # # # # # # # # # # #
	#								#
	#			CONFIGS				#
	#								#
	# # # # # # # # # # # # # # # # #

	version: 1.0.0
	update_check: true
	update-cooldown: 60
	# Attention ! L'auto-update peut détruire vos modifications !
	auto-update: false

	permission: skreload
	FileDirectory: /plugins/Skript/scripts

	contain-option: "c:"

	# # # # # # # # # # # # # # # # #
	#								#
	#			MESSAGES			#
	#								#
	# # # # # # # # # # # # # # # # #

	# Attention ! Ne pas enlever les " ou les variables !

	MVersion: &cReload ➣ &7La version &c%{_v}%&7 du fichier est disponible !
	MVersion2: &cReload ➣ &7Votre version actuelle est &c%{_u}%&7 !
	MUpdate: &bReload ➣ &7Mise à jour de skreload pour la version &b%{_v}%&7 en cours...
	MUpdated: &bReload ➣ &7Votre version de skreload vient d'être mise à jour sur la version &a%{_v}%&7 !

	MPermission: &cReload ➣ &7Tu n'as pas la permission d'executer cette commande !
	MNoName: &cReload ➣ &7Veuillez spécifier le nom d'un fichier à reload. &7(&c/skr [<Nom de fichier>]&7)

	MNoFile: &cReload ➣ &7Aucun fichier au nom de <&c%{_m}%&7> n'a été trouvé.
	MNoContain: &cReload ➣ &7Aucun fichier contenant <&c%{_m}%&7> n'a été trouvé.

	MReload: &bReload ➣ &7Reload en cours du fichier <&b%{skreload.last}%&7>...
	MEndReload: &aReload ➣ &7Reload du fichier &7<&a%{skreload.last}%&7> terminé. &7[Total: %{_c}%%{_total}/1000%%{_s}%&7]

	TTime: "&f&nTemps:"
	TPrefix: ➣
	TTotal: "&f&nTemps Total:"
	# Format des couleurs: (Temps en seconde)/(Code Couleur)
	# Séparation des temps avec un espace, possibilité de mettre les temps en virgule/point.
	# Veuillez mettre "none", si vous souhaitez ne pas avoir de format.
	TColor: 30/&4 10/&c 5/&6 2/&e 0/&a

every {@update-cooldown} seconds:
	set {_v} to line 2 from url "https://raw.githubusercontent.com/Frost145/Skript/SkriptReload/version"
	set {_u} to "{@version}"
	{_v} != "{@version}":
		{@update_check} = true
		loop all players:
			loop-player is op
			send "{@MVersion}" to loop-player
			send "{@MVersion2}" to loop-player
	{@auto-update} = true
	broadcast "{@MUpdate}"
	delete file "{@FileDirectory}/skr.sk"
	download file from "https://raw.githubusercontent.com/Frost145/Skript/SkriptReload/skr.sk" to file "{@FileDirectory}/skr.sk"
	reload skript "skr"
	broadcast "{@MUpdated}"

function TColor(n: number) :: text:
	loop split "{@TColor}" at " ":
		set {_i} to loop-value
		replace "," with "." in {_i}
		set {_t::*} to split "%{_i}%" at "/"
		{_n} >= {_t::1} parsed as number*10^3:
			set {_l::%{_t::1}%} to "%{_t::2}%"
	set {_k} to 0
	loop {_l::*}:
		loop-index parsed as number >= {_k}
		set {_k} to loop-index parsed as number
	{_l::%{_k}%} is not set:
		set {_l::%{_k}%} to "&a"
	return "%{_l::%{_k}%}%"

command /skreload [<text>]:
	aliases: skr
	permission: {@permission}
	permission message: {@MPermission}
	trigger:
		arg-1 is not set:
			send "{@MNoName}"
			stop
		set {_l} to {skreload.last}
		set {skreload.last} to arg-1
		arg 1 = "last":
			set {skreload.last} to {_l}
		set {_list::*} to split "%{skreload.last}%" at "/"
		broadcast "{@MReload}"
		{skreload.last} != "all" or "scripts":
			loop {_list::*}:
				loop-value contains {@contain-option}
				delete {_f}
				set {_c} to loop-value
				replace {@contain-option} with "" in {_c}
				loop files in dir including sub dirs "{@FileDirectory}":
					loop split loop-value-2 at "/":
						loop-value-3 contains ".sk"
						loop-value-3 contains "%{_c}%"
						first character of loop-value-3 != "-"
						set {_f} to loop-value-3
						replace ".sk" with "" in {_f}
						add {_f} to {_list::*}
				{_f} is not set:
					add loop-value to {_stop::*}
				remove loop-value from {_list::*}
			loop {_list::*}:
				delete {_0}
				loop-value != "config" or "aliases"
				loop files in dir including sub dirs "{@FileDirectory}":
					loop-value-2 contains "/%loop-value-1%.sk"
					set {_0} to true
				{_0} is not set
				add loop-value-1 to {_stop::*}
			{_stop::*} is set
			loop {_stop::*}:
				set {_n} to 1
				loop-value contains {@contain-option}
				add loop-value to {_stop2::*}
				remove loop-value from {_stop::*}
				delete {_n}
			{_n} is set:
				set {_m} to "%{_stop::*}%"
				replace ", " and " and " with "/" in {_m}
				broadcast "{@MNoFile}"
			{_stop2::*} is set:
				set {_m} to "%{_stop2::*}%"
				replace ", " and " and " with "/" in {_m}
				replace {@contain-option} with "" in {_m}
				broadcast "{@MNoContain}"
			loop {_stop::*}:
				remove loop-value from {_list::*}
			{_list::*} is not set
			set {skreload.last} to {_l}
			stop
		set {_tip} to {@TTime}
		{skreload.last} = "all" or "scripts":
			set {_list::*} to arg-1
		set {_s} to "s"
		loop {_list::*}:
			set {_t} to system nanoseconds
			execute sender command "/sk reload %loop-value%"
			set {_t} to (system nanoseconds-{_t})/10^6
			set {_c} to TColor({_t})
			add {_t} to {_total}
			set {_tip} to "%{_tip}%%nl%&7{@TPrefix} %loop-value%: %{_c}%%{_t}%ms &7(%{_c}%%{_t}/1000%%{_s}%&7)"
		set {_m} to "%{_list::*}%"
		replace ", " and " and " with "/" in {_m}
		set {skreload.last} to {_m}	
		set {_t} to {@TTotal}
		set {_c} to TColor({_total})
		set {_tip} to "%{_tip}%%nl%%nl%%{_t}%%{_c}% %{_total}%ms &7(%{_c}%%{_total}/1000%%{_s}%&7)"
		send formatted "<tooltip:%{_tip}%>{@MEndReload}" to all players
